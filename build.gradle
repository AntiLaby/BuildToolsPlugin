buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
  }
}

group 'de.heisluft.buildtools'
version '0.0.1'

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven {
    url 'https://heisluft.tk/maven/'
  }
  maven {
    url 'https://files.minecraftforge.net/maven/'
  }
}

dependencies {
  shadow gradleApi()
  compileOnly 'com.googlecode.json-simple:json-simple:1.2.0'
  compileOnly 'net.md-5:SpecialSource:1.8.5'
  compileOnly('com.custardsource.dybdob:java-diff-utils-copy:1.0.5') {
    transitive = false
  }
  compileOnly 'net.sf.jopt-simple:jopt-simple:5.0.4'
  compileOnly 'org.eclipse.jgit:org.eclipse.jgit:5.1.3.201810200350-r'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier.set('javadoc')
}


compileJava {
  options.encoding = 'UTF-8'
}

javadoc {
  options.encoding = 'UTF-8'
}

shadowJar {
  configurations = [project.configurations.compileOnly]
  archiveClassifier.set("")
}
publishing {
  repositories {
    maven {
      url = mavenRepo
      credentials {
        username = mavenUsername
        password = mavenPassword
      }
      authentication {
        digest(DigestAuthentication)
      }

    }
  }
  publications {
    mavenJava(MavenPublication) {
      pom {
        developers {
          developer {
            id = 'heisluft'
            name = 'heisluft'
            email = 'heisluftlp@gmail.com'
            roles = ['developer']
          }
        }
      }
      artifact sourcesJar
      artifact javadocJar
      from components.java
    }
  }
}

signing {
    sign publishing.publications.mavenJava
}

//Ensure that in a maven publication only the shaded jar is present
shadowJar.dependsOn jar
publishMavenJavaPublicationToMavenRepository.dependsOn test, shadowJar
publishMavenJavaPublicationToMavenLocal.dependsOn test, shadowJar

test {
  useJUnit()
}